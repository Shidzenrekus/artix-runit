# Maintainer: Denis Shestyora <shidzenrekus90@gmail.com>

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=zapret-runit
pkgver=20220407
pkgrel=1
pkgdesc="runit service script for zapret"
arch=('any')
url="https://github.com/Shidzenrekus/artix-runit"
license=('MIT')
depends=('runit' 'zapret')
provides=('init-zapret')
conflicts=('init-zapret')
source=("zapret.run" "zapret.finish")
sha256sums=('ec2d5d06dcfd0a6d622f0b564ff75148e609568264cc1598c0f9af6bfbe86092'
            '8bddbb724adf53206d0564592bfe9950cb32ed81714bcc0f29805b08427b76b7')

_inst_sv(){
    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'zapret'
}
