# Maintainer: Denis Shestyora <shidzenrekus90@gmail.com>

pkgname=zapret-git
pkgver=r457.6be8087
pkgrel=1
pkgdesc='Bypass deep packet inspection'
arch=(x86_64)
url="https://github.com/bol-van/zapret"
license=(MIT)
depends=('gcc' 'libnetfilter_queue' 'ipset' 'curl')
makedepends=('git')
provides=('zapret')
conflicts=('zapret')
backup=('opt/zapret/config')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
   cd "$srcdir/${pkgbase%-git}"
   (  set -o pipefail
      git describe --tags --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
   )
   pkgrel=$(git diff --shortstat|cut -d' ' -f2)
}

build() {
    make -C "$srcdir/${pkgbase%-git}"
}

package() {
    cd "$srcdir/${pkgbase%-git}"

    for n in ip2net mdig;do
        install -Dm755 "binaries/my/$n" "$pkgdir/opt/zapret/$n/$n"
    done
    install -Dm755 ipset/* -t "$pkgdir/opt/zapret/ipset"
    install -Dm755 init.d/sysv/* -t "$pkgdir/opt/zapret/init.d/sysv"
    install -Dm755 common/* -t "$pkgdir/opt/zapret/common"
    install -Dm644 config "$pkgdir/opt/zapret/config"
    install -Ddm755 "$pkgdir/usr/bin"    
    install -Dm755 "binaries/my/nfqws" "$pkgdir/opt/zapret/nfq/nfqws"
    ln -s /opt/zapret/nfq/nfqws "$pkgdir/usr/bin/nfqws"
    # Because nfqws work with HTTPS
    sed -ri "s/^#?MODE=.*$/MODE=nfqws/" "$pkgdir/opt/zapret/config"
}
 
