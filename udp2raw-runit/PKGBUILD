# Maintainer: Denis Shestyora <shidzenrekus90@gmail.com>

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=udp2raw-runit
pkgver=20220407
pkgrel=1
pkgdesc="runit service script for udp2raw"
arch=('any')
url="https://github.com/Shidzenrekus/artix-runit"
license=('MIT')
depends=('udp2raw' 'runit')
provides=('init-udp2raw')
conflicts=('init-udp2raw')
source=("udp2raw.run" "udp2raw_script.sh" "logudp2raw.run" "udp2raw.conf" "udp2raw.down")
sha256sums=('5b56894c04ab7d5acc88bdb135438f8788cd34489ae4e365ea9f96e67a913fb2'
            'd068e7461e81a2901d2e8b13af0c3bcd536cb8bebb650aa74d62aabf50c029a5'
            '80cc32ecae593c2a320a55ed1351f7206c15023775d5fe0d48425defd4dc53ce'
			'87a48cb5bfd6185dab78006463c01f0116cfc32837c1ade2d438f5ec2f8ff819'
			'32cd9d19162b5c02d7898fd8c8cdcb34fcc7c398e97f18c892973f7a0e16a51f')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv(){
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    if test -f "$srcdir/$1.down"; then
        install -Dm755 "$srcdir/$1.down" "$pkgdir/etc/runit/sv/$1/control/d"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'udp2raw'
    _inst_logsv 'udp2raw'
}
