# Maintainer: Denis Shestyora <shidzenrekus90@gmail.com>

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=whoogle-runit
pkgver=20220322
pkgrel=1
pkgdesc="runit service script for whoogle"
arch=('any')
url="https://github.com/Shidzenrekus/artix-runit"
license=('MIT')
depends=('whoogle' 'runit')
provides=('init-whoogle')
conflicts=('init-whoogle')
backup=('etc/runit/sv/whoogle/conf')
source=("whoogle.run" "whoogle.conf" "logwhoogle.run")
sha256sums=('ddfa25ed099af9f37fb4b610bfd926ecbfd3aeee7c9e0a8a8d53dcf867227797'
            '1a591a8d3bceb3ca30a9cf48ff2bbbcc946423251a1b6b2ff3367b800364dd66'
            '869c6d729cde99c6b54f1de68ab536b31947f9af397eb9e6285b2d50630dc50e')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv(){
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'whoogle'
    _inst_logsv 'whoogle'
}
